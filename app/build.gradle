apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion configs.compileSdkVersion
    buildToolsVersion  configs.buildToolsVersion
    defaultConfig {
        applicationId  configs.applicationId
        minSdkVersion  configs.minSdkVersion
        targetSdkVersion  configs.targetSdkVersion
        versionCode  configs.versionCode
        versionName  configs.versionName
//        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            // No debug config
        }

        release {
            storeFile file("zhihudaily.jks")
            storePassword "123456"
            keyAlias "ZHIHUDAILY"
            keyPassword "123456"
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件 true 时 混淆必须打开
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.all {
                    def apkName = 'v' + defaultConfig.versionName + '_' + releaseTime()
                    if (!variant.flavorName.isEmpty()) {
                        apkName += '_' + variant.flavorName
                    }
                    outputFileName = apkName + '.apk'
                }
            }
        }
    }

    flavorDimensions "default"
    // 友盟多渠道打包
    productFlavors {
        // ./gradlew assembleRelease
        _test_20200602_1 { dimension "default" }
        ceshi { dimension "default" }
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation files('libs/lite-orm-1.9.2.jar')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_version"
    testImplementation "junit:junit:4.12"
    implementation "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit2_version"
    implementation("com.trello.rxlifecycle2:rxlifecycle-components:$rxlifecycle2_version", {
        exclude group: "com.google.code.findbugs"
        exclude group: "com.android.support"
    })
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.bigkoo:convenientbanner:$convenientbanner_version"
    implementation "com.facebook.fresco:fresco:$fresco_version"
    implementation "de.hdodenhof:circleimageview:$circleimageview_version"
}